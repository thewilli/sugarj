/**
 * dryad-compiler main strategies.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module org/sugarj/dryad/compiler/dryad-compiler

imports
  libstratego-lib
  org/sugarj/dryad/compiler/dryad
  libjava-front
  org/sugarj/dryad/compiler/dryadcompiler/backend/-
  org/sugarj/dryad/compiler/dryadcompiler/backend/asm/-
  org/sugarj/dryad/compiler/dryadcompiler/backend/asm/optimize/-
  org/sugarj/dryad/compiler/dryadcompiler/backend/asm/transform/-
  org/sugarj/dryad/compiler/dryadcompiler/backend/asm/instructions/-
  org/sugarj/dryad/compiler/dryadcompiler/backend/emit/-
  org/sugarj/dryad/compiler/dryadcompiler/backend/unasm/-
  org/sugarj/dryad/compiler/dryadcompiler/frontend/-
  org/sugarj/dryad/compiler/dryadcompiler/util/-
  org/sugarj/dryad/compiler/dryadcompiler/util/trace/-
  org/sugarj/dryad/compiler/dryadcompiler/lib-ext/-
  org/sugarj/languages/Java-Bytecode

strategies

  // default onfailure argument; see below
  dryad-compiler          = dryad-compiler      (dryad-compiler-error)
  dryad-compiler-back     = dryad-compiler-back (dryad-compiler-error)
  
  /**
   * Parse and compile a (list of) XTC input(s) or String(s);
   * mixed Java classes/class files to regular classes.
   *
   * @param onfailure  Strategy that gets an DryadError or DryadWarning term for
   *                   each error or warning, may fail the entire strategy.
   *                   May also be used to emit constructs from a language
   *                   extension.
   *                   Default: dryad-compiler-error
   *
   * @see dryad-compiler-options
   * @see dryad-compiler-output   Writes the compiler results to stdout.
   */
  dryad-compiler(onfailure) =
    dryad-compiler-input-wrap(
      dryad-compiler-front(onfailure); // add type annotations
  
      if not(<get-config> NoVerify) then // (internal option)
        dryad-compiler-front-errors(onfailure)
      end;
    
      if Stage-gt(|1) then
        dryad-compiler-back(onfailure)
      end;
      
      if Stage-lt(|3); <get-config> NoDebugInfo then
        strip-trace-tags;
        strip-position-annos
      end
    )
    
  /**
   * Backend to dryad-compiler-front or similar strategies;
   * compiles *type-annotated* Java/bytecode inputs.
   *
   * @param onfailure  Strategy that gets an DryadError or DryadWarning term for
   *                   each error or warning, may fail the entire strategy.
   *                   May also be used to emit constructs from a language
   *                   extension.
   *                   Default: dryad-compiler-error
   *
   * @see dryad-compiler
   * @see dryad-compiler-output   Writes the compiler results to stdout.
   */
  dryad-compiler-back(onfailure) =
    as-list; // always output a list, but the input doesn't have to be one
  
    // Emit 1 or more classes for each source class
    log-timed-risky(dryad-emit(onfailure) |"emit code", 1);
    
    if Stage-gt(|3) then
      log-timed-risky(
        flatten-list(write-class-file)    |"output to binary class format", 1
      )
    end

/**
 * Java/dryad library strategies.
 *
 * @author Lennart Kats <lennart add lclnet.nl>
 */
module dryad-compiler/lib-ext/java

imports libstratego-lib
        dryad

strategies

  /** @type Field -> Field Object */
  lookup-field =
    ?Field(_, Id(name));
    declaring-class-attr; 
    log-lookup-class;
    get-declared-field(|name)

  /** @type Lit -> String */
  from-java-string =
    ?Lit(String(<id>));
    map(from-java-string-part);
    concat-strings

  /** @type Lit -> Int */
  from-java-char =
    ?Lit(Char(<id>));
    from-java-string-part;
    explode-string;
    (\[x, y] -> <add> (y, <mul> (x,256))\ + ?[<id>])
  +
    ?Lit(Char(Single(<id>)))  

  /** @internal */
  from-java-string-part = // TODO: Proper Java string unescaping (JLS 3.10.5)
   ?Chars(<id>)
  <+
   if is-OctaEscape + is-UnicodeEscape then
     warn(|"Octal and unicode escapes not yet supported.")
   end;
   java-to-box => S(<id>); unescape // borrow pp-code

  is-loop-stm = ?While(_, _) + ?DoWhile(_, _) + ?For(_, _, _, _) + ?ForEach(_, _, _)

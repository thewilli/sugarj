/**
 * Access to Java bytecode by conversion to ATerms.
 *
 * @todo Use libstratego-sglr for parsing signatures
 * @author Martin Bravenboer <martin@cs.uu.nl>
 * @author Lennart Kats <l.c.l.kats@tudelft.nl>
 */
module org/sugarj/dryad/compiler/dryad/bytecode/Bridge
imports
  org/sugarj/dryad/compiler/libstratego-jvm
  org/sugarj/dryad/compiler/dryad/bytecode/Signature
  org/sugarj/dryad/compiler/dryad/bytecode/ClassSignature
  org/sugarj/dryad/compiler/dryad/bytecode/MethodSignature
  org/sugarj/dryad/compiler/dryad/bytecode/FieldSignature
  
signature constructors
  Result : A -> B

strategies

  getJars = id
    ; !["aterm.jar","aterm-java-1.8.2.jar","bcel-5.2.jar","IntermediateLanguage-1.0.jar","jjtraveler.jar","shared-objects.jar","aterm2class.jar"]
    ; map(\x -> <concat-strings>["aterm2class/",x]\)
    ; map(ResolveResource)


  strjvm-callstatic-new(|class,method) = id
    ; as-list
    ; map(try(\x -> String(x) where is-string\))
    ; CallJavaStatic(|<getJars>,class,method)
    ; ?Result(String(<id>))

  read-class-file =
    ?Jar(jarpath, filepath)
    ; <strjvm-callstatic-new(|"str/classtree/Class2ATerm", "disasm_from_jar")> [jarpath, filepath]
    ; parse-signatures
    
  read-class-file =
    (?ClassFile(path) + ?FILE(path))
    ; <strjvm-callstatic-new(|"str/classtree/Class2ATerm", "disasm")> [path, "off"]
    ; parse-signatures
    
  read-class-file-disasm = 
    (?ClassFile(path) + ?FILE(path))
    ; <strjvm-callstatic-new(|"str/classtree/Class2ATerm", "disasm")> [path, "on"]
    ; parse-signatures
    
  /**
   * Serializes a Java class file to a file.
   * The output file name and path is determined from the package and class name.
   *
   * @type ClassFile -> FILE
   */
  write-class-file:
    classfile @ ClassFile(_, _, _, ThisClass(name), _, _, _, _, _) -> FILE(output)
    where
      path         := <add-extension> (<string-replace(|".", "/")> name, "class")
    ; FILE(output) := <write-class-file(|path)> classfile

  /**
   * Serializes a Java class file to a file.
   *
   * @type ClassFile -> FILE
   */
  write-class-file(|path):
    ClassFile(_, _, _, _, _, _, _, _, _) -> FILE(path)
    where
      write-to
    ; xtc-transform(!"baffle", !["-ws"]) => FILE(input)
    ; where(get-jvm(|[<id> "class2aterm.jar"]) => jvm)    
    ; <strjvm-callstatic-new(|"str/classtree/ATerm2Class", "asm")> [input, path]

strategies  
    
  get-jvm(|classpath) =
    GetJVM
    <+ strjvm-create(|classpath) => jvm
       ; rules(GetJVM: _ -> jvm)

strategies

  parse-signatures =
    topdown(try(
      ?ClassSignature(<parse-class-signature>)
    + ?MethodSignature(<parse-method-signature>)
    + ?FieldSignature(<parse-field-signature>)
    ))

  parse-class-signature =
    parse-signature(|<ResolveResource>"ClassSignature.tbl")

  parse-method-signature =
    parse-signature(|<ResolveResource>"MethodSignature.tbl")
    
  parse-field-signature =
    parse-signature(|<ResolveResource>"FieldSignature.tbl")

  parse-signature(|tbl) =
    where(<get-signature-parse-table> tbl => opentbl)
    ; (parse-string(|opentbl) <+ report-parse-error; fail)
    ; desugar-signature

  get-signature-parse-table =
    ?tbl
    ; ( ParseTable
        //<+ <xtc-find> tbl
        <+ !tbl
           ; ReadFromFile
           ; open-parse-table => opentbl
           ; rules(ParseTable: tbl -> opentbl)
      )

   desugar-signature =
    alltd(
      ( ?ClassSignature(_, _, _)
      + ?MethodSignature(_, _, _, _)
      + ?FieldSignature(_)
      )
      ; topdown(repeat(DesugarSignature))
    )
    
  DesugarSignature :
    MethodSignature(Some(t1), t2, t3, t4) -> MethodSignature(t1, t2, t3, t4)
    
  DesugarSignature :    
    MethodSignature(None(), t2, t3, t4) -> MethodSignature(TypeParams([]), t2, t3, t4)
    
  DesugarSignature :
    ClassSignature(Some(t1), t2, t3) -> ClassSignature(t1, t2, t3)

  DesugarSignature :
    ClassSignature(None(), t2, t3) -> ClassSignature(TypeParams([]), t2, t3)
    
  DesugarSignature :
    TypeName(Id(x)) -> TypeName(PackageName([]), Id(x))
    
  DesugarSignature :
    TypeArg(None(), type) -> type

  /**
   * @todo Desugar None ClassBound
   */  
  DesugarSignature :
    TypeParam(Id(x), ClassBound(Some(ftype)), is)
      ->
    TypeParam(Id(x), Some(TypeBound([ftype | is])))
    
  DesugarSignature :
    Class(type) -> type

  /**
   * @todo Temporary hack to support ? extends E by rewriting to E.
   */    
  DesugarSignature :    
    TypeArg(Some(Plus), TypeVar(Id(x))) -> TypeArg(None(), TypeVar(Id(x)))
